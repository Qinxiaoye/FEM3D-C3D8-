!write the fix node to 'nodeforce.dat'
!WRITE BY BINGBINGXU 
!Modified on $Date: 2018-4-8 15:24:31  (Sun, 8  Mar 2018) 
!COM *********************************************************
!COM                                                         *
!COM PARAMETERS:                                             *
!COM       there is one parameter need to input              *
!COM       ARG1                                              *
!COM                	ndim = arg1                            *
!COM     Only one element type can exist in the model        *
!COM *********************************************************
!COM

*cfopen,nodeForce,DAT,,
*vwrite
 0 0 0
*if,arg1,lt,2,then
	ndim = 2
*else
	ndim = arg1 
*endif        
SUMNODE=ndinqr(0,13) ! function "NDINQR(NODE,KEY)" can return number of 
                     !selected nodes if node==0, key==13
_node = 0
*do,i,1,sumNode
	_NODE=NDNEXT(_NODE)
	*get,fx,node,_node,F,fx
	*get,fy,node,_node,F,fy
	*if,ndim,eq,3,then
		*get,fz,node,_node,F,fz
	*endif
	
	*if,abs(fx),GT,0.0001,then
		*vwrite,_NODE,1,fx
		%8I%8I%20.4G
	*ENDIF
	*if,abs(fy),GT,0.0001,then
		*vwrite,_NODE,2,fy
		%8I%8I%20.4G
	*endif

	*if,ndim,eq,3,then
		*if,abs(fz),Gt,0.0001,then
			*vwrite,_NODE,3,fz
			%8I%8I%20.4G
		*endif
	*endif

*enddo
*cfclos

i=
a=
sumNode = 
fx=
fy=
fz=
ndim=